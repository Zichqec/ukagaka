//Written by Zichqec https://ukagaka.zichqec.com/
//Used to make a nice looking display for real numbers (floats) in YAYA, since they are always forced to display with 6 trailing digits.
//By default, this function will cut off any trailing 0s. (If there are no non-zero digits after the decimal point, it will display a single 0 by default, like "1.0". You can make it display as a whole number if desired.)
//Note that the output is returned as a string. This is just for display purposes! Like in a stats menu, and such.

//Argument 0: The real number to display
//Argument 1: The number of digits to display after the decimal point, by force. This is optional, and is used if you want it to, say, always display 3 digits after the decimal point, whether they are 0s or not. YAYA only tracks up to 6 digits after the decimal, but if you specify more for some reason this function will add additional 0s to match the length you specified.
//Argument 2: Whether numbers with no non-zero digits after the decimal point should be displayed like "1.0" or like "1" (whole number). 0 or blank to display like "1.0", 1 to display as a whole number. This setting only applies if argument 2 is blank.

//Examples:
//DisplayReal(Temperature,2)
//DisplayReal(Friendship,,1) //← Note the blank argument 1 here

DisplayReal
{
	//By default, chop all trailing 0s
	//If an argument is passed, include that many trailing 0s
	_input = TOSTR(_argv[0])
	_first += _input[0,"."] //First part of number plus decimal
	_second = _input[1,"."]
	_output = ""
	
	if _argv[1] != "" //If a number of places to leave is included
	{
		_output += SUBSTR(_second,0,_argv[1])
		
		if _argv[1] > 6 //Add more 0s if they want more than 6 for some reason
		{
			for _i = 0; _i < _argv[1] - 6; _i++
			{
				_output += "0"
			}
		}
		_first + "." + _output
	}
	else
	{
		if TOINT(_second) == 0
		{
			if _argv[2] == 1
			{
				_first
			}
			else
			{
				_first + "." + "0"
			}
			return
		}
		
		_pos = 5
		for _i = 5; _i >= 0; _i--
		{
			_pos = _i
			if SUBSTR(_second,_i,1) != "0"; break
		}
		
		_first + "." + SUBSTR(_second,0,_pos + 1)
	}
}