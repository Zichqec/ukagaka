//Written by Zichqec https://ukagaka.zichqec.com/
//Takes an array of alternating names and urls, and formats them for use with the right click menu
FormatLinks : all
{
	//Loop jumps by 2 so that it works with pairs. Url can be grabbed by using _i + 1
	for _i = 0; _i < ARRAYSIZE(_argv); _i += 2
	{
		"%(_argv[_i])%(CHR(1))%(_argv[_i + 1])%(CHR(2))"
	}
}

//When you write your links, you'll have to make a new array function to store them in. All you have to do is write separate strings for each title and link, title first. Here are some examples of ways you can format it:

//Names/links on separate lines, one after another
recommendlinks : array
{
	"Site 1"
	"https://example.com/"
	"Site 2"
	"https://example.org/"
	"Site 3"
	"https://example.net/"
}

//Name/link pairs, with a semicolon separating them
portallinks : array
{
	"Site 1"; "https://example.com/"
	"Site 2"; "https://example.org/"
	"Site 3"; "https://example.net/"
}

//The event for the top tab in the right click menu, as an example of how this is implemented
On_sakura.recommendsites
{
  FormatLinks(recommendlinks)
}